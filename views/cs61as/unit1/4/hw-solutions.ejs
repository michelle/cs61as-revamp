CS 61A                        Homework 4 solutions

2.7
<pre>
(define (upper-bound interval)
  (cdr interval))

(define (lower-bound interval)
  (car interval))
</pre>
2.8

;; The smallest possible value for A-B is (smallest A)-(largest B).
;; Likewise, the largest value is (largest A)-(smallest B).
<pre>
(define (sub-interval x y)
  (add-interval x (make-interval (- (upper-bound y))
                                 (- (lower-bound y)))))
</pre>
;; It would also be okay to replicate the structure of add-interval instead of
;; using add-interval as a subprocedure, although this isn't exactly following
;; Alyssa's method as the problem suggests:
<pre>
(define (sub-interval x y)
  (make-interval (- (lower-bound x) (upper-bound y))
                 (- (upper-bound x) (lower-bound y)) ))
</pre>

2.10

;; An interval spans zero if its lower bound is negative (or zero) and
;; its upper bound is positive (or zero).
;; It's only the divisor that we have to worry about.
<pre>
(define (div-interval x y)
  (if (and (<= (lower-bound y) 0)
           (>= (upper-bound y) 0))
      (error "Can't divide by an interval that spans zero.")
      (mul-interval x
                    (make-interval (/ 1 (upper-bound y))
                                   (/ 1 (lower-bound y))))))
</pre>


2.12
<pre>
(define (make-center-percent c p)
  (let ((w (* c p 0.01)))
    (make-interval (- c w) (+ c w))))
</pre>
;; We multiply by 0.01 because p percent is a factor of p/100.
;; If you forgot about that part, it's a relatively minor error
;; for computer science purposes, although rather serious in a
;; practical engineering situation!
<pre>
(define (percent i)
  (* 100 (/ (/ (- (upper-bound i) (lower-bound i)) 2)
            (/ (+ (lower-bound i) (upper-bound i)) 2))))
</pre>
;; Slightly more efficient percent:
<pre>
(define (percent i)
  (* 100 (/ (- (upper-bound i) (lower-bound i))
            (+ (lower-bound i) (upper-bound i)))))
</pre>
;; Alternate versions, using the center-width procedures we already have:
<pre>
(define (make-center-percent c p)
  (make-center-width c (* c p 0.01)))

(define (percent i)
  (* 100 (/ (width i) (center i))))
</pre>

2.17
<pre>
(define (last-pair lst)
  (if (null? (cdr lst))
      lst
      (last-pair (cdr lst))))
</pre>

2.20

The difficulty in writing recursive procedures that take any number of
arguments is that you're going to be tempted to make a recursive call
with only one argument, namely a list containing some of the original
arguments, sort of like this:
<pre>
(define (same-parity . numbers)
  (cond ((null? (cdr numbers)) numbers)
        ((equal? (even? (car numbers))
                 (even? (cadr numbers)))
         (cons (car numbers)
               (same-parity (cdr numbers))))   ; WRONG!
        (else (same-parity (cons (car numbers) (cddr numbers))))))   ; WRONG!

(define (even? num)
  (= (remainder num 2) 0))
</pre>
Instead, the easiest thing to do is to define a helper procedure that
*does* expect a list of numbers as its one argument.  An advantage is
that we can then separate out the first number, which is always
accepted, as a special case:
<pre>
(define (same-parity tester . others)
  (define (helper numlist)
    (cond ((null? numlist) nil)
          ((equal? (even? tester) (even? (car numlist)))
           (cons (car numlist) (helper (cdr numlist))))
          (else (helper (cdr numlist)))))
  (cons tester (helper others)))
</pre>
Once we know about higher-order functions, there's an even easier
solution:
<pre>
(define (same-parity tester . others)
  (cons tester
        (filter (lambda (num) (equal? (even? tester) (even? num)))
                others)))
</pre>
Note that using KEEP would be incorrect, since your argument is a list,
not a sentence!


2.22.  What's wrong with iterative square-list?

I've sort of answered this already, in talking about 2.18 (iterative
reverse).  A list (as opposed to an arbitrary list structure) MUST be made
with
<pre>
(cons new-member rest-of-list)
</pre>
and not the other way around.  That's why Louis's second try fails; he's
making something that isn't a list, even though he does have the members
in correct left-to-right order.  He gets
<pre>
((((() . 1) . 4) . 9) . 16)
</pre>
instead of <pre>(1 4 9 16)</pre>, which in dotted representation would be
<pre>
(1 . (4 . (9 . (16 . ()))))
</pre>
Louis's first try fails because he starts by consing the square of 1 onto
the empty list, etc.

Here's an iterative square-list:
<pre>
(define (square-list x)
  (define (iter list answer)
    (if (null? list)
        answer
        (iter (cdr list)
              (cons (square (car list))
                    answer))))
  (reverse (iter x nil)))
</pre>

In this version, iter is O(n) time and O(1) space, as desired.  If we
use the iterative definition of reverse, it too is O(n) time and O(1) space.
So the whole thing is reasonably efficient in both time and space, even
though it has what seems to be a redundant pass through the list to reverse
it.  If you're dealing with very large lists on very small computers, this
is sometimes the only way to get an answer at all.


2.23
<pre>
(define (for-each proc lst)
  (if (null? lst)
      #t
      (let ((ignored-result (proc (car lst))))
        (for-each proc (cdr lst)))))
</pre>
Several people said, "Why not just use map?"  There are two answers.
First, it's inefficient because map will construct a list of all the
results, and we don't care about the results.  Second, map does not
guarantee to apply the procedure argument to the list elements in
left-to-right order.

If you think it's inelegant to have a variable ignored-result whose
value is something we don't care about, you're right.  In chapter 3
we'll learn about an alternative notation:
<pre>
(define (for-each proc lst)
  (if (null? lst)
      #t
      (begin (proc (car lst))
             (for-each proc (cdr lst)))))
</pre>
And it turns out that a COND clause can contain more than two expressions,
so this can also be written as
<pre>
(define (for-each proc lst)
  (cond ((null? lst) #t)
        (else (proc (car lst))
              (for-each proc (cdr lst)))))
</pre>

SUBSTITUTE:

For reference, here's the SUBSTITUTE for sentences from week 2 lab:
<pre>
(define (substitute sent old new)
  (cond ((empty? sent) '())
        ((equal? (first sent) old)
         (se new (substitute (butfirst sent) old new)))
        (else (se (first sent) (substitute (butfirst sent) old new)))))
</pre>

If we just change the sentence constructor/selector functions to the proper
ones for lists, and one formal parameter's name, we get this:
<pre>
(define (substitute LST old new)        ;; first try
  (cond ((NULL? LST) '())
        ((equal? (CAR LST) old)
         (CONS new (substitute (CDR LST) old new)))
        (else (CONS (CAR LST) (substitute (CDR LST) old new)))))
</pre>
Why CONS rather than LIST or APPEND?  Because we're combining some function of
the CAR of a pair with some function (namely a recursive call) of the CDR,
trying to build a new structure with the same shape as the original.  LIST or
APPEND would change the shape; if we used LIST, all the lists we made would
have exactly two elements, but if we used APPEND, we would be flattening
sublists into, essentially, a sentence.

But this isn't good enough, because it doesn't examine sublists.  The most
obvious solution would be to add a clause to the COND to distinguish sublists
from atomic (non-list) elements:
<pre>
(define (substitute lst old new)
  (cond ((null? lst) '())
        ((equal? (car lst) old)
         (cons new (substitute (cdr lst) old new)))
        ((PAIR? (CAR LST))
         (CONS (SUBSTITUTE (CAR LST) OLD NEW)
               (SUBSTITUTE (CDR LST) OLD NEW)))
        (else (cons (car lst) (substitute (cdr lst) old new)))))
</pre>
This works.  But once we're familiar with the idioms of tree recursion
(week 6), we can see a slightly more elegant solution:
<pre>
(define (substitute lst old new)
  (cond ((null? lst) '())
        ((equal? LST old) NEW)
        ((pair? LST)
         (cons (substitute (car lst) old new)
               (substitute (cdr lst) old new)))
        (else LST)))
</pre>
The ELSE clause is reached when the LST argument is a word rather than
a list, and is not equal to OLD.  (A list is either null or a pair.)

(Aside: why do we use PAIR? instead of LIST? to see if the element is a
sublist? It doesn't really matter, but PAIR? is more efficient. LIST? has
to follow all the cdrs to make sure that the last cdr is the empty list.
Here, we're just assuming that everything in the top-level list is either a
well-formed sublist, or a single element (a word). The final version of
substitute will even work on arbitrary pair structures.)

SUBSTITUTE2:

This will have the same program structure as SUBSTITUTE, but the EQUAL? test
is replaced by something a little more complicated.

The straightforward solution is to use MEMBER in place of EQUAL?, and then, if
a true answer is returned, look for the right thing to substitute:
<pre>
(define (substitute2 lst olds news)
  (cond ((null? lst) '())
        ((MEMBER lst olds)
         (FIND-MATCH LST OLDS NEWS))
        ((pair? lst)
         (cons (substitute2 (car lst) olds news)
               (substitute2 (cdr lst) olds news)))
        (else lst)))

(define (find-match element olds news)
  (cond ((null? olds) #f)        ; This shouldn't happen
        ((equal? element (car olds))
         (car news))
        (else (find-match element (cdr olds) (cdr news)))))
</pre>
This works fine.  But it's a little inelegant that we end up looking for the
element in OLDS twice, once with MEMBER and once again with FIND-MATCH.  We
can photograph two birds with one stone if we replace the first COND clause
of find-match with one that returns the value we actually want if ELEMENT
isn't found in OLDS:
<pre>
(define (substitute2 lst olds news)
  (cond ((null? lst) '())
        ((pair? lst)
         (cons (substitute2 (car lst) olds news)
               (substitute2 (cdr lst) olds news)))
        (else (FIND-MATCH LST OLDS NEWS))))

(define (find-match element olds news)
  (cond ((null? olds) ELEMENT)
        ((equal? element (car olds))
         (car news))
        (else (find-match element (cdr olds) (cdr news)))))
</pre>
This version of SUBSTITUTE2 has one fewer COND clause.  The two clauses
for lists remain, and the ELSE clause now handles all words, whether or
not they're members of OLDS.



Extra for experts:
<pre>
(define (cxr-function name)
  (define (helper name)
    (if (empty? name)
        (lambda (x) x)
        (compose (if (equal? (first name) 'a) car cdr)
                 (helper (bf name)))))
  (helper (bf (bl name))))        ; eliminate C and R from name

(define (compose f g)
  (lambda (x) (f (g x))))
</pre>
If you didn't think to use COMPOSE, you could of course do the same thing
without it, a little less neatly:
<pre>
(define (cxr-function name)
  (define (helper name)
    (if (empty? name)
        (lambda (x) x)
        (lambda (x) ((if (equal? (first name) 'a) car cdr)
                     ((helper (bf name)) x)))))
  (helper (bf (bl name))))        ; eliminate C and R from name
</pre>

2.6 (Church numerals):
<pre>
(define one (lambda (f) (lambda (x) (f x))))
</pre>
Or, equivalently,
<pre>
(define one (lambda (f) f))
</pre>
But the longer definition makes it easier to see how the pattern extends:
<pre>
(define two (lambda (f) (lambda (x) (f (f x)))))
(define three (lambda (f) (lambda (x) (f (f (f x))))))
</pre>
You may find the nested lambdas easier to understand if we look at one
of these in abbreviated-DEFINE form:
<pre>
(define (three f)
  (lambda (x) (f (f (f x)))))
</pre>
So, a number is a /function of a function/ (not surprising, since functions
are all we have).  The argument X to the returned function is, of course,
also a function, but it'll usually be a function that represents a number,
whereas F might be a function /whose domain/ is numbers, such as ADD-1.
This business of keeping track of the /abstract/ type of various things
all of which are, concretely, just functions, is what makes this a hard
project to understand.


Here are the arithmetic functions.  It's ironic that the intuitively "lower"
functions are the harder to implement in this context:
<pre>
(define (plus a b)
  (lambda (f) (lambda (x) ((a f) ((b f) x)))))

(define (times a b)
  (lambda (f) (a (b f))))

(define (expt a b)
  (b a))
</pre>
We use the names PLUS and TIMES to avoid messing up regular arithmetic
on regular numbers using + and *.


If you want to play with Church numerals, it'll be very convenient to
have this tool to translate from Church to ordinary numbers:
<pre>
(define (try num)                        ; convert Church to
  ((num (lambda (x) (+ x 1))) 0))        ; ordinary number
</pre>
Since this uses +, 1, and 0, it's not valid as part of the Church numeral
system itself, but for debugging, we can use it to see what the result of
some operation was:
<pre>
> (try (plus two three))
5
</pre>

Before we can invent subtraction, we need some supporting features:
conditionals and pairs.

For conditionals, we start by defining equivalents to #T and #F:
<pre>
(define true (lambda (a b) a))
(define false (lambda (a b) b))
</pre>
Now, if only we had normal order evaluation (needed for the reasons you
explored in the NEW-IF exercise), we could do this:
<pre>
; (define (if pred t f) (pred t f))
</pre>
Why should this work?  Substitute TRUE for PRED and you get this:
<pre>
((lambda (a b) a) t f)
</pre>
which indeed returns the argument T, and similarly for FALSE.  The trouble is
that both T and F are evaluated before this version of IF is called.  To fix
this, we have to make it a special form, with the following piece of magic:
<pre>
(define-macro (iff pred t f)
  `((,pred (lambda () ,t) (lambda () ,f))))
</pre>
(This is STk-specific; it might not work in another Scheme dialect, although
they all have ways to define macros.)

Now we can write a simple predicate:
<pre>
(define (zero? num)
  (lambda (a b)
    ((num (lambda (x) b))
     a)))
</pre>
What does this mean?  A predicate takes the form
<pre>
        (lambda (a b)
          [expression that returns A to mean true and B to mean false])
</pre>
The function <pre>(LAMBDA (X) B)</pre> ignores its argument X, and always returns B.
Composing this function with itself, any number of times, still gives a
function that ignores its argument and returns B.  So (NUM (LAMBDA (X) B)),
which composes (LAMBDA (X) B) with itself NUM times, is a function that
returns B -- /unless/ NUM is zero.  Recall that ZERO is defined this way:
<pre>
        (define zero (lambda (f) (lambda (x) x)))
</pre>
So if NUM is ZERO, its argument (F in the expression above, which is
<pre>(LAMBDA (X) B)</pre> in our case) is ignored, and NUM returns the identity function.

So, what are we doing with the result of <pre>(NUM (LAMBDA (X) B))</pre>?  We're invoking
it with the argument A!  So if NUM isn't ZERO, the result will be B, and if
NUM /is/ ZERO, the result will be A.  This is just what we needed to test
whether NUM is ZERO!

We'll invent more predicates later, but this will do for now.

Here's an equivalent to TRY for debugging conditionals:
<pre>
(define (trypred pred)                        ; convert Church to
  (pred #t #f))                                ; ordinary predicate
</pre>
For example:
<pre>
> (trypred (zero? three))
#F
</pre>
For the next step we must invent pairs (as a way to provide two arguments to a
one-argument function):
<pre>
(define (kons a b) (lambda (msg) (msg a b)))
(define (kar pair) (pair (lambda (a b) a)))
(define (kdr pair) (pair (lambda (a b) b)))
</pre>
This is like the redefinition of pairs shown in lecture, except that instead
of the symbols CAR and CDR, we use functions as the messages, namely the
function that returns its first argument for CAR, and the one that returns
its second argument for CDR.

You'll notice that this implementation of pairs is exactly like the
implementation of conditionals above!  Pairs and conditionals are two
abstract data types implemented the same way, just like many such examples
in the text and lectures.


Now we can use pairs to subtract 1 from a number.  This is the hardest thing
to invent, the core of the whole project:
<pre>
(define (-1+ num)
  (lambda (f)
    (lambda (x)
      (kdr ((num (lambda (pair) (kons f ((kar pair) (kdr pair)))))
            (kons (lambda (z) z) x))))))
</pre>
What does this mean?  Well, basically, since a number is a function, the
only thing we can do to it is invoke it on some argument.  So we're invoking
NUM on a function whose domain and range are both pairs.  And what the
number does to that function is invoke it repeatedly on some argument.
Here's a table showing the result:

                        car of pair                cdr of pair
                        ------------------        ------------
initial argument        identity function        X
after 1st invocation        F                        (ID X) = X
after 2nd invocation        F                        (F X)
after 3rd invocation        F                        (F (F X))
after 4th invocation        F                        (F (F (F X)))

... so after NUM invocations, we've composed NUM-1 invocations of F.
That's exactly what (-1+ NUM) means.  The trick is that we call
(KAR PAIR) NUM times, not NUM-1 times, but we nullify the first invocation
because that first time, (KAR PAIR) is the identity function.  All the
other times, (KAR PAIR) is F.

Once we can subtract 1 from a number, we can subtract anything by repeatedly
subtracting 1:
<pre>
(define (minus x y)
  ((y -1+) x))
</pre>
Beyond this point we need recursion, so we invent the Y combinator:
<pre>
(define (Y f) (lambda (x) (f f x)))
(define (YY f) (lambda (x y) (f f x y)))
</pre>
And now we can do just about anything straightforwardly:
<pre>
(define =?
  (YY (lambda (f x y)
        (iff (zero? x)
             (iff (zero? y)
                  true
                  false)
             (iff (zero? y)
                  false
                  (f f (-1+ x) (-1+ y)))))))

(define fact
  (Y (lambda (f n)
       (iff (zero? n)
            one
            (times n (f f (-1+ n)))))))
</pre>

The Boolean functions (as regular procedures, not special forms):
<pre>
(define (both p q)        ; and
  (p q false))

(define (either p q)        ; or
  (p true q))

(define (knot p)
  (p false true))
</pre>
Here we take advantage of the fact that minus returns zero if the result
should be negative:
<pre>
(define (lesseq a b)
  (zero? (minus a b)))

(define (greatereq a b)
  (lesseq b a))

(define (less a b)
  (both (lesseq a b) (knot (=? a b))))

(define (greater a b)
  (less b a))
</pre>
Alternatively, we can use lesseq to create a non-recursive equal test:
<pre>
(define (equal a b)
  (both (lesseq a b) (greatereq a b)))

(define (less a b)
  (both (lesseq a b) (knot (greatereq a b))))
</pre>
Technically, we're cheating by using DEFINE, but if we really use just
LAMBDA, the resulting code is unreadable.  Here's (FACT FIVE):
<pre>
> (try
   (((lambda (f) (lambda (x) (f f x)))                        ; ((Y
     (lambda (f n)                                        ;   (lambda (f n)
       (((lambda (pred t f) (pred t f))                        ;    (if
         ((lambda (num)                                        ;     (zero?
            (lambda (a b)
              ((num (lambda (x) b))
               a)))
          n)                                                ;      n)
         (lambda () (lambda (f) f))                        ;     one
         (lambda () ((lambda (a b)                        ;     (times
                       (lambda (f) (a (b f))))
                     n                                        ;      n
                     (f f ((lambda (num)                ;      (f f (-1+
                             (lambda (f)
                               (lambda (x)
                                 ((lambda (pair) (pair (lambda (a b) b)))
                                  ((num (lambda (pair)
                                          ((lambda (a b)
                                             (lambda (msg) (msg a b)))
                                           f
                                           (((lambda (pair)
                                               (pair (lambda (a b) a)))
                                             pair)
                                            ((lambda (pair)
                                               (pair (lambda (a b) b)))
                                             pair)))))
                                   ((lambda (a b)
                                      (lambda (msg) (msg a b)))
                                    (lambda (z) z)
                                    x))))))
                           n))))))))                        ;            n))))))
    (lambda (f) (lambda (x) (f (f (f (f (f x)))))))))        ;  five)
120
</pre>